%# BEGIN BPS TAGGED BLOCK {{{
%# 
%# COPYRIGHT:
%# 
%# This software is Copyright (c) 1996-2008 Best Practical Solutions, LLC
%#                                          <jesse@bestpractical.com>
%# 
%# (Except where explicitly superseded by other copyright notices)
%# 
%# 
%# LICENSE:
%# 
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org.
%# 
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%# 
%# You should have received a copy of the GNU General Public License
%# along with this program; if not, write to the Free Software
%# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
%# 02110-1301 or visit their web page on the internet at
%# http://www.gnu.org/licenses/old-licenses/gpl-2.0.html.
%# 
%# 
%# CONTRIBUTION SUBMISSION POLICY:
%# 
%# (The following paragraph is not intended to limit the rights granted
%# to you to modify and distribute this software under the terms of
%# the GNU General Public License and is only of importance to you if
%# you choose to contribute your changes and enhancements to the
%# community by submitting them to Best Practical Solutions, LLC.)
%# 
%# By intentionally submitting any modifications, corrections or
%# derivatives to this work, or any other work intended for use with
%# Request Tracker, to Best Practical Solutions, LLC, you confirm that
%# you are the copyright holder for those contributions and you grant
%# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
%# royalty-free, perpetual, license to use, copy, create derivative
%# works based on those contributions, and sublicense and distribute
%# those contributions and any derivatives thereof.
%# 
%# END BPS TAGGED BLOCK }}}
<%args>
$query => "id > 0"
$primary_group_by => 'queue'
$secondary_group_by => undef
$chart_style => 'bars'
</%args>
<%init>
my @keys;
my @values;
my $chart_class;
use GD;
use GD::Text;

if ($chart_style eq 'pie') {
    require GD::Graph::pie;
    $chart_class = "GD::Graph::pie";
} else {
    require GD::Graph::bars;
    $chart_class = "GD::Graph::bars";
}

my $tix = RT::Report::Tickets->new();
$tix->from_sql( $query );
my $count_name = $tix->column( FUNCTION => 'COUNT', column => 'id' );
$tix->group_by( column => $primary_group_by );
my $value_name = $tix->column( column => $primary_group_by );

my $chart = $chart_class->new( 600 => 400 );

my $font = RT->config->get('chart_font') || ['verdana', 'arial', gdMediumBoldFont];
$chart->set_title_font( $font, 12 ) if $chart->can('set_title_font');
$chart->set_legend_font( $font, 12 ) if $chart->can('set_legend_font');
$chart->set_x_label_font( $font, 10 ) if $chart->can('set_x_label_font');
$chart->set_y_label_font( $font, 10 ) if $chart->can('set_y_label_font');
$chart->set_label_font( $font, 10 ) if $chart->can('set_label_font');
$chart->set_x_axis_font( $font, 9 ) if $chart->can('set_x_axis_font');
$chart->set_y_axis_font( $font, 9 ) if $chart->can('set_y_axis_font');
$chart->set_values_font( $font, 9 ) if $chart->can('set_values_font');
$chart->set_value_font( $font, 9 ) if $chart->can('set_value_font');

# Pie charts don't like having no input, so we show a special image
# that indicates an error message. Because this is used in an <img>
# context, it can't be a simple error message. Without this check,
# the chart will just be a non-loading image.
if ($tix->count == 0) {
    my $plot = GD::Image->new(600 => 400);
    $plot->color_allocate(255, 255, 255); # background
    my $black = $plot->color_allocate(0, 0, 0);

    require GD::Text::Wrap;
    my $error = GD::Text::Wrap->new($plot,
        color => $black,
        text  => _("No tickets found."),
    );
    $error->set_font( $font, 12 );
    $error->draw(0, 0);

    $m->comp( 'SELF:Plot', plot => $plot, %ARGS );
}

if ($chart_class eq "GD::Graph::bars") {
    $chart->set(
        x_label => $tix->label( $primary_group_by ),
        x_labels_vertical => 1,
        y_label => _('Tickets'),
        show_values => 1
    );
}

my %class = (
    queue => 'RT::Model::Queue',
    Owner => 'RT::Model::User',
    Creator => 'RT::Model::User',
    LastUpdatedBy => 'RT::Model::User',
);
my $class = $class{ $primary_group_by };

while ( my $entry = $tix->next ) {
    if ( $class ) {
        my $q = $class->new();
        $q->load( $entry->__value( $value_name ) );
        push @keys, $q->name;
    }
    else {
        push @keys, $entry->__value($value_name);
    }

    $keys[-1] ||= _('(no value)');
    if ($chart_class eq 'GD::Graph::pie') {
        $keys[-1] .= " - ". $entry->__value( $count_name );
    }
    push @values, $entry->__value($count_name);
}

# XXX: Convert 1970-01-01 date to the 'Not Set'
# this code should be generalized!!!
if ( $primary_group_by =~ /(Daily|Monthly|Annually)$/ ) {
    my $re;
    $re = qr{1970-01-01} if $primary_group_by =~ /Daily$/;
    $re = qr{1970-01} if $primary_group_by =~ /Monthly$/;
    $re = qr{1970} if $primary_group_by =~ /Annually$/;
    foreach (@keys) {
        s/^$re/_('Not Set')/e;
    }   
}

unless (@keys && @values) {
    @keys = ('');
    @values = (0);
}

my %data;
my %loc_keys;
foreach my $key (@keys) { $data{$key} = shift @values; $loc_keys{$key} = _($key); }
my @sorted_keys = map { $loc_keys{$_}} sort { $loc_keys{$a} cmp $loc_keys{$b} } keys %loc_keys;
my @sorted_values = map { $data{$_}} sort { $loc_keys{$a} cmp $loc_keys{$b} } keys %loc_keys;

my $plot = $chart->plot( [ [@sorted_keys], [@sorted_values] ] ) or die $chart->error;
$m->comp( 'SELF:Plot', plot => $plot, %ARGS );
</%init>

<%METHOD Plot>
<%ARGS>
$plot => undef
</%ARGS>
<%INIT>
my @types = ('png', 'gif');

for my $type (@types) {
    $plot->can($type)
        or next;

    $r->content_type("image/$type");
    $m->out( $plot->$type );
    $m->abort();
}

die "Your GD library appears to support none of the following image types: " . join(', ', @types);
</%INIT>

</%METHOD>
